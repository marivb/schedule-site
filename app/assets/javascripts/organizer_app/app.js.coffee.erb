app = angular.module('organizerApp', ['ngRoute', 'ngResource', 'ui.bootstrap'])

app.config [
  '$routeProvider', '$httpProvider',
  ($routeProvider, $httpProvider) ->
    $routeProvider
      .when('/schedules/:scheduleId',
        templateUrl: '<%= asset_path('organizer_app/organize_schedule.html') %>'
        controller: 'OrganizeScheduleCtrl'
      )

    authToken = $('meta[name="csrf-token"]').attr('content')
    $httpProvider.defaults.headers.common['X-CSRF-TOKEN'] = authToken
]

app.factory 'Schedule', [
  '$resource',
  ($resource) ->
    $resource('/api/schedules/:id')
]

app.factory 'Session', [
  '$resource',
  ($resource) ->
    $resource('/api/schedules/:scheduleId/sessions/:sessionId',
        {scheduleId: '@scheduleId', sessionId: '@id'})
]

app.controller 'OrganizeScheduleCtrl', [
  '$scope', '$routeParams', '$modal', 'Schedule', 'Session',
  ($scope, $routeParams, $modal, Schedule, Session) ->
    Schedule.get id: $routeParams.scheduleId, (schedule) ->
      for time in schedule.times
        time.session = { blank: true }
      $scope.schedule = schedule
    Session.query scheduleId: $routeParams.scheduleId, (sessions) ->
      $scope.sessions = {}
      $scope.sessions[session.id] = session for session in sessions

    currentlyEditting = undefined
    $scope.editing = (time) -> time == currentlyEditting
    $scope.edit = (time) -> currentlyEditting = time
    $scope.addSessionToTime = (time, sessionId) ->
      time.session = $scope.sessions[sessionId]
      currentlyEditting = undefined
    $scope.clear = (time) ->
      time.session = { blank: true }
      currentlyEditting = undefined

    $scope.newSession = ->
      modalInstance = $modal.open(
        templateUrl: '<%= asset_path('organizer_app/new_session.html') %>'
        controller: 'NewSessionCtrl'
        scope: $scope
      )
      modalInstance.result.then (session) ->
        $scope.sessions.push session
]

app.controller 'NewSessionCtrl', [
  '$scope', '$modalInstance', 'Session',
  ($scope, $modalInstance, Session) ->
    $scope.session = new Session({scheduleId: $scope.schedule.id})

    $scope.createSession = ->
      $scope.session.$save ->
        session = $scope.session
        $scope.session = undefined
        $modalInstance.close(session)

    $scope.cancel = ->
      $modalInstance.dismiss('cancel')
]
