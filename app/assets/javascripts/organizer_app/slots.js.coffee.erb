module = angular.module('organizer.slots', [])

module.directive 'scheduleSlot', [
  'Sessions', 'Editor', 'Scheduler',
  (Sessions, Editor, Scheduler) ->
    restrict: 'A'
    templateUrl: '<%= asset_path('organizer_app/slot.html') %>'
    scope: { time: '=', slot: '=', session: '=', sessions: '=' }
    link: ($scope, element, attributes) ->
      if $scope.slot.type == 'session'
        $scope.$watch 'session', ->
          element.attr('rowspan', $scope.session.timeSlotSpan) if $scope.session?

    controller: [
      '$scope',
      ($scope) ->
        $scope.control = {
          remove: ->
            Scheduler.clearSlot $scope.time, $scope.slot, $scope.session
            Editor.clear()

          add: (session) ->
            Scheduler.addSession $scope.time, $scope.slot, session

          edit: ->
            Editor.edit($scope.slot)

          reset: (form) ->
            $scope.sessionToAdd = ''
            form.$setPristine()
            Editor.clear()
        }
        return
    ]
]

module.directive 'focusOn', ($timeout) ->
  restrict: 'A'
  link: (scope, element, attrs) ->
    scope.$watch attrs.focusOn, (value) ->
      if value
        $timeout -> element[0].focus()
