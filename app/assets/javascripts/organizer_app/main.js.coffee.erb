module = angular.module('organizer.main',
                       ['organizer.sessions', 'organizer.scheduler', 'organizer.slots',
                        'organizer.rooms', 'organizer.sidebar', 'ui.bootstrap', 'ngResource'])

module.config [
  '$httpProvider',
  ($httpProvider) ->
    authToken = $('meta[name="csrf-token"]').attr('content')
    $httpProvider.defaults.headers.common['X-CSRF-TOKEN'] = authToken
]

module.service 'Editor', [
  ->
    currentlyEditting = undefined
    @edit = (slot) ->
      currentlyEditting.editting = false if currentlyEditting
      currentlyEditting = slot
      slot.editting = true
    @clear = ->
      currentlyEditting.editting = false if currentlyEditting
      currentlyEditting = undefined

    return
]

module.controller 'OrganizeScheduleCtrl', [
  '$scope', 'Scheduler', 'Sessions', 'Editor', 'Rooms'
  ($scope, Scheduler, Sessions, Editor, Rooms) ->
    $scope.loadSchedule = ->
      Scheduler.load (schedule) -> $scope.schedule = schedule
      Sessions.load (sessions) -> $scope.sessions = sessions
      Rooms.load (rooms) -> $scope.rooms = rooms
      $scope.editor = Editor

    $scope.sessionFor = (slot) ->
      Sessions.find(slot.sessionId) if slot.type == 'session'

    $scope.addRoom = ->
      Scheduler.addRoom()
]
