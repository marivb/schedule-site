module = angular.module('organizer.main',
                       ['organizer.sessions', 'organizer.scheduler',
                        'organizer.sidebar', 'ui.bootstrap', 'ngResource'])

module.config [
  '$httpProvider',
  ($httpProvider) ->
    authToken = $('meta[name="csrf-token"]').attr('content')
    $httpProvider.defaults.headers.common['X-CSRF-TOKEN'] = authToken
]

module.service 'Editor', [
  'Sessions',
  (Sessions) ->
    currentlyEditting = undefined
    @editing = (time) ->
      time == currentlyEditting
    @edit = (time) ->
      currentlyEditting = time
    @clear = ->
      currentlyEditting = undefined

    return
]

module.directive 'focusOn', ($timeout) ->
  restrict: 'A'
  link: (scope, element, attrs) ->
    scope.$watch attrs.focusOn, (value) ->
      if value
        $timeout -> element[0].focus()

module.controller 'OrganizeScheduleCtrl', [
  '$scope', 'Scheduler', 'Sessions', 'Editor'
  ($scope, Scheduler, Sessions, Editor) ->
    $scope.loadSchedule = ->
      Scheduler.load (schedule) -> $scope.schedule = schedule
      Sessions.load (sessions) -> $scope.sessions = sessions
      $scope.editor = Editor

    $scope.sessionFor = (sessionId) ->
      Sessions.find(sessionId)
    $scope.addSessionToTime = (time, session) ->
      Scheduler.add time, time.slots[0], session
      Editor.clear()
    $scope.clear = (time) ->
      Scheduler.clear time, time.slots[0]
      Editor.clear()
]
