module = angular.module('organizer.main',
                       ['organizer.sessions', 'organizer.scheduler', 'organizer.slots',
                        'organizer.sidebar', 'ui.bootstrap', 'ngResource'])

module.config [
  '$httpProvider',
  ($httpProvider) ->
    authToken = $('meta[name="csrf-token"]').attr('content')
    $httpProvider.defaults.headers.common['X-CSRF-TOKEN'] = authToken
]

module.service 'Editor', [
  ->
    currentlyEditting = undefined
    @edit = (time) ->
      currentlyEditting.editting = false if currentlyEditting
      currentlyEditting = time
      time.editting = true
    @clear = ->
      currentlyEditting.editting = false if currentlyEditting
      currentlyEditting = undefined

    return
]

module.controller 'OrganizeScheduleCtrl', [
  '$scope', 'Scheduler', 'Sessions', 'Editor'
  ($scope, Scheduler, Sessions, Editor) ->
    $scope.loadSchedule = ->
      Scheduler.load (schedule) -> $scope.schedule = schedule
      Sessions.load (sessions) -> $scope.sessions = sessions
      $scope.editor = Editor

    $scope.sessionFor = (sessionId) ->
      Sessions.find(sessionId)
    $scope.addSessionToTime = (time, session) ->
      Scheduler.addSession time, time.slots[0], session
      Editor.clear()
    $scope.clear = (time, session) ->
      Scheduler.clearSlot time, time.slots[0], session
      Editor.clear()
]
